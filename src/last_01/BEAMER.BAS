2 DIM O(130,2):GOTO 2500
3 PRINT:GOSUB 17:PRINT
5 GOSUB 100:GOSUB 2000:PRINT
7 GOSUB 600:PRINT
9 GOSUB 710
10 PRINT:INPUT "...ENTER CASE # 1-4 ",Z
12 PRINT
13 IF Z=4 OR Z=3 THEN GOTO 610
14 IF Z=1 THEN GOTO 35
15 IF Z=2 THEN GOTO 107
16 GOTO 3
17 PRINT"1. FIXED-FIXED BEAM":PRINT"   <A><-C->":PRINT"   I1 #####-Q 2I"
18 PRINT"   ============="
20  PRINT "   I<----L---->I"
21 RETURN :REM copy
35 GOSUB 17
37 LET D=0
40 GOSUB 900
47 LET D=A+C
50 MA=-Q/12/L/L*(6*L*L*(D*D-A*A)-8*L*(D^3-A^3)+3*(D^4-A^4))
60 LET MB=-Q/12/L/L*(4*L*(D^3-A^3)-3*(D^4-A^4))
70 LET VA=Q*C/L*(L-A-C/2)+(MB-MA)/L
75 LET VB=Q*C-VA
80 GOSUB 572
84 LET X=0
85 LET DF=VA*X^3/6+MA*X^2/2
86 LET MX=VA*X+MA
87 IF X<=A OR X>A+C THEN GOTO 90
88 LET MX=MX-Q/2*(X-A)^2
89 LET DF=DF-Q/2*(X^4/12-X^3*A/3+A*A*X*X/2+A^4/12-A^3*X/3)
90 IF X<=A+C THEN GOTO 93
91 LET MX=(Q*C-VA)*(L-X)+MB
92 LET DF=VB/2*(L*X^2-X^3/3-L^2*X+L^3/3)+MB*(-L*X+X*X/2+L*L/2)
93 LET K=K+1
94 GOSUB 3000
95 LET X=X+J:IF X<=L THEN GOTO 85
96 GOSUB 3200
97 IF T5=1 THEN GOTO 85
98 GOSUB 2000
99 GOTO 850
100 PRINT"2. PINNED-FIXED BEAM":PRINT"    D=L CANTILEVER"
101  PRINT   "    <A><-C->":PRINT"       #####-Q  I"
102  PRINT"    ============="
103  PRINT"    <-D->I1    2I"
105 PRINT"    <----L----->I"
106 RETURN
107 GOSUB 100
108 GOSUB 900
109 LET VA=C*Q
110 LET D1=D
111 LET TR=0
112 IF D<L THEN GOSUB 300
114 IF D<L THEN LET VA=T*3/(L-D)^3
115 LET TR=1
127 IF Z=2 THEN GOSUB 572
130 LET X=0
135 LET MX=0
140 IF X>A+0 THEN LET MX=-Q/2*(X-A)^2
145 IF X>A+C THEN LET MX=-C*Q*(X-A-C/2)
150 IF X>D THEN LET MX=MX+VA*(X-D)
152 IF X<=D THEN LET DF=VA*(L-D)^2/6*(3*L-3*X-L+D)
153 IF X>D THEN LET DF=VA*(L-X)^2/6*(3*(L-D)-L+X)
156 LET D=X
157 GOSUB 300
158 LET DF=DF-T
159 LET  K=K+1
160 IF Z=2 THEN GOTO 166
163 LET O(K,1)=MX
164 LET O(K,2)=DF
165 GOTO 169
166 GOSUB 3000
169 LET D=D1
170 LET X=X+J:IF X<=L THEN GOTO 135:REM sinclair for-next logic
171 GOSUB 3200
172 IF T5=1 THEN GOTO 135
174 IF Z=3 OR Z=4 THEN GOTO 620
175 GOSUB 2000
180 GOTO 850
300 IF D<A+C THEN GOTO 310
304 LET T=C*Q*(L-D)^2*((L-D)/3-A/2-C/4+D/2)
309 GOTO 323
310 IF D<A+0 THEN D=A
312 LET Y=A+C-D
313 LET T=Q/2*Y*Y*(Y*Y/4+2*(D-A)*Y/3+(D-A)^2/2)
316 LET Y=L-D
319 LET T=T+C*Q*Y*Y*(Y/3-A/2-C/4+D/2)
321 LET Y=A+C-D
322 LET T=T-C*Q*Y*Y*(Y/3-A/2-C/4+D/2)
323 LET D=D1
325 IF TR=1 THEN LET D=X
365 IF D<A+0 THEN T=T+(A-D)*C/2*Q*((A-L)*(A-L+C)+C^2/3)
380 RETURN
400 PRINT
401 LET T4=0
403 LET T3=0
404 LET K=0
408 DIM R(130,2)
409 DIM P(130,2)
412 PRINT "EXPLAINATION OF VARIABLE"
413 PRINT "------------------------"
415 GOSUB 100
416 LET V3=0
417 LET V4=0
418 LET V5=0
420 PRINT
422 PRINT "L=LENGTH OF BEAM."
423 PRINT"D=DISTANCE TO PIN SUPPORT."
424 PRINT "A=DISTANCE TO LOAD (Q)."
430 PRINT "C=LENGTH OF LOAD."
431 PRINT"Q=INTENSITY OF LOAD."
432 GOSUB 2000:PRINT
433 PRINT "  IF D=L; CANTILEVER."
434 PRINT"V1=SHEAR AT POINT 1"
436 PRINT: PRINT " UNITS: FEET, LBS./FT."
438 PRINT
440 PRINT"FOR CONCETRATED LOAD LET C=.1 OR .05 AND INCREASE 'Q' ACCORDINGLY."
444 PRINT"EXAMPLE FOR P=100 LBS.  LET C=.1 AND Q=1000 OR LET C=.05 AND Q=2000."
445 PRINT"----------------------"
450 GOSUB 2000:PRINT
452 PRINT"MODULUS OF ELASTICITY":PRINT"1. STEEL E=30,000 KSI"
453 PRINT"2. WOOD  E=1,760 KSI"
456 PRINT"3. OTHER":PRINT:PRINT"..ENTER #1-3";
472 INPUT Z$
473 LET Z=VAL(Z$)
474 IF Z>3 THEN GOTO 472
475 IF Z=1 THEN LET E=30000000#
476 IF Z=2 THEN LET E=1.76E+06
478 IF Z=1 OR Z=2 THEN GOTO 490
479 PRINT:IF Z<>3 THEN GOTO 472
480 INPUT"INPUT E (KSI) ? ",E
490 PRINT "E=";E;" (KSI)"
500 REM print
510 PRINT:INPUT"INPUT MOMENT OF INERTIA (I) OF THE BEAM (INCH^4) ? ",I
525 PRINT "   I=";I;" (INCH^4)"
526 INPUT "INPUT ELASTIC SECTION-MODULUS (Sx=I/C)  (INCH^3) ? ",SX:PRINT "   Sx=";SX;" (INCH^3)"
530 PRINT:INPUT"INPUT TABULATION INTERVAL (FEET) ? ",J
536 PRINT "   TAB. INTERVAL=";J;"FT."
538 PRINT "----------------------------"
539 GOSUB 2000
540 GOTO 3
572 PRINT
573 PRINT"===================================":PRINT "V1=";VA;" (LBS.)  V2=";Q*C-VA
574 PRINT:PRINT " TAB";TAB(9);"MOMENT";TAB(20);"DEFLECTION";TAB(34);"STRESS"
575 PRINT "(FT.)";TAB(9);"(FT.-LBS.)";TAB(22); "(FEET)";TAB(35);"(PSI)"
576 PRINT "-----"; TAB(9); "----------"; TAB(22);"------";TAB(35);"-----"
577 RETURN
600 PRINT"3. PINNED-PINNED BEAM":PRINT"    <A><-C->":PRINT"       #####-Q"
601  PRINT"    ============="
602  PRINT"    <-D->I1    2I"
604 PRINT"    <-----L----->"
607 RETURN
610 IF Z = 4 THEN  GOTO 716
611 GOSUB 600
615 GOTO 108
620 LET V2 = O(K,1) /(L-D)
622 LET K = 0
625 LET X=0
626 LET K=K+1
636 IF X>D+0 THEN LET O(K,1)=O(K,1)-V2*(X-D)
652 IF X<D+0 THEN LET DF=((L-X)^3-(D-X)^3)/3+(X-D)/2*((L-X)^2-(D-X)^2)
655 IF X>=D THEN LET DF=(L-X)^2 *(L/3-D/2+X/6)
660 LET DF=V2*((L-D)^2/3*(L-X)-DF)
670 LET O(K,2)=O(K,2)+DF
673 LET X=X+J:IF X<=L THEN GOTO 626:REM sinclair logic
674 GOSUB 3200
675 IF T5=1 THEN GOTO 626
676 LET VA=VA-V2
678 IF T3=1 THEN GOTO 780
679 IF Z=4 THEN GOTO 740
680 GOSUB 572
681 LET K=0
682 LET X=0
683 LET K=K+1
684 LET MX=O(K,1)
685 LET DF=O(K,2)
686 GOSUB 3000
688 LET X=X+J:IF X<=L THEN GOTO 683:REM sinclair for-next logic
689 GOSUB 3200
690 IF T5=1 THEN GOTO 683
692 GOSUB 2000
695 GOTO 850
700 REM
710 PRINT"4. PIN-PIN-PIN BEAM":PRINT"    <A><-C->":PRINT"       #####-Q"
711  PRINT"    ============="
712  PRINT"  1I<-D->I2    3I"
714 PRINT"    <-----L----->"
715 RETURN
716 GOSUB 700
717 GOSUB 900
720 LET J1=1
721 IF INT(D/J)<>0 THEN LET J1=INT(D/J)
722 LET J=D/J1
724 LET W=C*Q
725 LET D2=D
730 LET D=0
735 GOTO 109
740 LET V1=VA
750 LET VB=-O(J1+1,2)*3*L/D2^2/(L-D2)^2
755 LET C=.001
756 LET A=D2-.0005
757 LET D=0
758 LET Q=VB*1000
760 LET K=0
762 LET X=0
763 LET K=K+1
764 LET P(K,1)=O(K,1)
766 LET P(K,2)=O(K,2)
767 LET X=X+J:IF X<=L THEN GOTO 763:REM sinclair logic
768 GOSUB 3200
769 IF T5=1 THEN GOTO 763
770 LET K=0
772 LET T3=1
774 GOTO 109
780 LET VA=V1-VA
781 LET D=D2
783 PRINT"================================":PRINT "V1=";VA;" (LBS.)  V2=";VB;"V3=";W-VA-VB
784 GOSUB 574
785 LET K=0
787 LET X=0:REM sinclair logic
790 LET K=K+1
791 LET MX=P(K,1)-O(K,1)
792 LET DF=P(K,2)-O(K,2)
793 GOSUB 3000
794 LET X=X+J:IF X<=L THEN GOTO 790:REM sinclair logic
795 GOSUB 3200
796 IF T5=1 THEN GOTO 790
797 LET T3=0
798 GOSUB 2000
850 PRINT:PRINT "DO YOU WANT TO SUPER-IMPOSE ANOTHER LOAD ON THIS BEAM (Y/N) ";
852 LET V3=V3+VA
854 IF Z =4 THEN LET V4=V4+VB
856 IF Z=1 OR Z=2 OR Z=3 THEN LET V4=V4+C*Q-VA
858 IF Z=4 THEN LET V5=V5+W-VA-VB
860 INPUT G$
865 IF G$<>"y" AND G$<>"Y" THEN GOTO 1000
870 LET K=0
871 LET T4=1
872 PRINT
874 IF Z=1 THEN GOSUB 17
876 IF Z=2 THEN GOSUB 100
878 IF Z=3 THEN GOSUB 600
880 IF Z=4 THEN GOSUB 710
882 PRINT TAB(13) ;"L=";L
884 IF Z>1 THEN PRINT TAB(13); "D=";D
886 GOTO 910
890 IF Z=2 OR Z=3 THEN GOTO 109
891 IF Z=1 THEN GOTO 47
892 IF Z=4 THEN GOTO 724
900 PRINT:PRINT "INPUT (FT.)";:INPUT "L=";L
901 PRINT"L=";L
903 IF Z=1 THEN GOTO 910
904 INPUT "D=";D
905 PRINT"D=";D
907 GOTO 911
910 REM
911 INPUT "A=";A
912 PRINT "A=";A
913 INPUT "C=";C
915 PRINT "C=";C
917 INPUT "LOAD (LBS./FT.) Q=";Q
918 PRINT "LOAD (LBS./FT.) Q=";Q
940 IF A+C>L OR D>L THEN PRINT "----ERROR A+C>L OR D>L"
946 IF D=L AND Z=3 THEN PRINT "----ERROR D=L BEAM NOT STACTIC"
947 IF (D=L OR D=0)AND Z=4 THEN PRINT"ERROR D=L OR D=0 FOR PIN-PIN BEAM USE CASE 3"
948 IF (D=L AND Z=3) OR (Z=4 AND (D=L OR D=0)) OR D>L THEN GOSUB 2000:GOTO 540
950 IF A+C<=L THEN GOTO 955
951 GOSUB 2000
952 GOTO 872
955 IF T4=1 THEN GOTO 890
960 RETURN
1000 PRINT :REM cls
1010 PRINT "COMBINED EFFECT OF ALL LOADS"
1020 PRINT:PRINT "V1=";V3;" (LBS.)  V2=";V4
1030 IF Z=4 THEN PRINT "V3=";V5
1040 GOSUB 574
1050 LET K=0
1060 LET X=0:REM SINCLAIR FOR-NEXT LOGIC
1070 LET K=K+1
1080 LET MX = R(K,1)
1085 LET DF=R(K,2)
1086 GOSUB 3000
1090 LET X=X+J:IF X<=L THEN GOTO 1070:REM sinclair for next logic
1095 GOSUB 3200
1096 IF T5=1 THEN GOTO 1070
1100 GOSUB 2000
1110  PRINT "---END OF RUN---"
1112 END
2000 PRINT:INPUT "PRESS <RETURN> KEY " ;G$:RETURN
2500 PRINT:PRINT " CIVIL ENGINEERING SYSTEM"
2505 PRINT:PRINT"BEAMER V1.00 - BEAM ANALYSIS":PRINT:PRINT"     COPYRIGHT 1983"
2507 PRINT"ALL RIGHTS RESERVED BY:"
2510 PRINT" US SOFTWARE COMPANY":PRINT"    P.O. Box 805":PRINT" SHELTON, CT. 06484"
2530 PRINT"======================"
2535 PRINT "THIS PROGRAM WILL PRINT MOMENT AND DEFLECTION"
2536 PRINT "VALUES ALONG PRISMATIC  BEAMS."
2540 GOSUB 2000
2550 GOTO 400
3000 DFF=-DF*144/E/I
3001 PRINT USING "###.## ########.#  ##.#####  ########.#";X,MX,DFF,MX*12/SX
3002 IF INKEY$="" THEN 3005
3003 IF INKEY$="" THEN 3003
3005 LET R(K,1)=R(K,1)+MX
3006 LET R(K,2)=R(K,2)+DF
3007 RETURN
3200 LET T5=0
3210 IF ABS(X-J-L) <.001 THEN RETURN
3215 LET X=L
3220 LET T5=1
3240 RETURN
